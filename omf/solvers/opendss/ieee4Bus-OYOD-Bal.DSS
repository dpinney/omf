!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
!-------This file contains two scripts demonstrating two different ways of handling the floating unbalanced delta side -------
!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------

! The DSS uses a nodal admittance solution technique with a voltage reference of remote earth (ground).
! Sometimes, on delta systems there is insufficient stray capacitance of lines to stabilize the neutral point and
! convergence is more difficult. 
! The DSS attempts to prevent this by adding a little inductive shunt conductance to ground in the transformer model.
! This test case is one of the rare examples where this reactance appears to create a shunt resonance with the
! 2500 ft of line capacitance, resulting in unusual convergence properties. (Try it with the default parameters to see what
! I mean). The solution converges just fine with PPM_Antifloat=0 (no extra shunt conductance). Default is 1. 
! (see user's manual and on-line wiki). If you can always guarantee there will be sufficient conductance to ground to 
! establish a ground reference on the delta side of the transformer, set PPM=0 for transformers.

! -------------- Roger Dugan

!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------

!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
!-----------------------------------------------------First Script (change PPM_Antifloat value) -----------------------------------------------------------
!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
clear
Set DefaultBaseFrequency=60

! IEEE 4-bus test case   Open Y - Open Delta Stepdown Balanced
! Based on a script developed by Alan Dunn and Steve Sparling for the YY-Balanced case

new circuit.4busOYODBal   basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** TWO 1-PHASE STEP-DOWN TRANSFORMERS 7.2/4.16 KV CONNECTED IN OY-OD BANK
! See test case document for polarities for connection
! The delta side of the transformer tends to float around with respect to ground, making convergence more difficult
! This example rectifies that problem by setting the ppm_antifloat property of the transformers = 0. 
! The alternate script (below) uses a Y-connected Reactor to stabilize the neutral point on the delta side.
new transformer.t1 xhl=6 Phases=1
~ wdg=1 bus=n2.1.0     conn=wye     kV=7.2   kVA=2000 %r=0.5  
~ wdg=2 bus=n3.1.2     conn=Delta   kV=4.16  kVA=2000 %r=0.5 
~ ppm=0      ! anti float not needed for this example (sufficient line capacitance for ground reference)

new transformer.t2 xhl=6 Phases=1
~ wdg=1 bus=n2.2.0     conn=wye     kV=7.2   kVA=2000 %r=0.5 
~ wdg=2 bus=n3.2.3     conn=Delta   kV=4.16  kVA=2000 %r=0.5 
~ ppm=0       ! anti float not needed for this example (sufficient line capacitance for ground reference)


! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  

! **** Delta-CONNECTED 4.16 KV LOAD 3600 kVA
new load.load1 phases=3 bus1=n4 conn=Delta kV=4.16 kW=3600 pf=0.9  model=1
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage    

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve

! Various reports ...
//Show Voltage  LL Nodes
//show voltages LN Nodes 
//show voltages     ! You can see zero sequence voltages with this report and infer neutral shift (about 6%)
//show currents resid=yes elements
//Show Powers kva Elements      ! Note: Phase powers on delta side have little meaning



!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------
!----------------------------Alternate Script (Uses small stabilizing reactor on delta side)-----------------------------------------------------------
!----------------------------------------------------------------------------------------------------------------------------------------------------------------------------

clear

! IEEE 4-bus test case   Open Y - Open Delta Stepdown Balanced
! Based on script developed by Alan Dunn and Steve Sparling

new circuit.4busOYODBal   basekV=12.47 phases=3 
! **** HAVE TO STIFFEN THE SOURCE UP A LITTLE; THE TEST CASE ASSUMES AN INFINITE BUS
~ mvasc3=200000 200000

! **** DEFINE WIRE DATA 
new wiredata.conductor Runits=mi Rac=0.306 GMRunits=ft GMRac=0.0244  Radunits=in Diam=0.721 
new wiredata.neutral   Runits=mi Rac=0.592 GMRunits=ft GMRac=0.00814 Radunits=in Diam=0.563 

! **** DEFINE LINE GEOMETRY; REDUCE OUT THE NEUTRAL
new linegeometry.4wire nconds=4 nphases=3 reduce=yes 
~ cond=1 wire=conductor units=ft x=-4   h=28 
~ cond=2 wire=conductor units=ft x=-1.5 h=28 
~ cond=3 wire=conductor units=ft x=3    h=28 
~ cond=4 wire=neutral   units=ft x=0    h=24 

! **** 12.47 KV LINE
new line.line1 geometry=4wire length=2000 units=ft bus1=sourcebus bus2=n2

! **** TWO 1-PHASE STEP-DOWN TRANSFORMERS 7.2/4.16 KV CONNECTED IN OY-OD BANK
! See test case document for polarities for connection

new transformer.t1 xhl=6 Phases=1
~ wdg=1 bus=n2.1.0     conn=wye     kV=7.2   kVA=2000 %r=0.5  
~ wdg=2 bus=n3.1.2     conn=Delta   kV=4.16  kVA=2000 %r=0.5 

new transformer.t2 xhl=6 Phases=1
~ wdg=1 bus=n2.2.0     conn=wye     kV=7.2   kVA=2000 %r=0.5 
~ wdg=2 bus=n3.2.3     conn=Delta   kV=4.16  kVA=2000 %r=0.5 

! **** 4.16 KV LINE
new line.line2 bus1=n3 bus2=n4 geometry=4wire length=2500 units=ft  

! **** Delta-CONNECTED 4.16 KV LOAD 3600 kVA
new load.load1 phases=3 bus1=n4 conn=Delta kV=4.16 kW=3600 pf=0.9  model=1
! **** HAVE TO ALLOW P, Q TO REMAIN CONSTANT TO ABOUT .79 PU -- THIS IS ASSUMED IN TEST CASE
! **** DEFAULT IN DSS IS .95, BELOW WHICH IT REVERTS TO LINEAR MODEL
~ vminpu=0.75    ! model will remain const p,q down to 0.75 pu voltage    


! Add 1-kvar Reactor to stabilize the neutral point on the Delta Side
! New Reactor.Stabilizer Bus1=N4  kV=4.16 kvar=1  Conn=wye  R=0
New Capacitor.Stabilizer Bus1=N4  kV=4.16 kvar=1  Conn=wye  R=0
! (a capacitor would also suffice)

set voltagebases=[12.47, 4.16] 
calcvoltagebases     ! **** let DSS compute voltage bases
solve

! Various reports ...
//Show Voltage  LL Nodes
//show voltages LN Nodes    ! Voltages are more realistic in this script
//show voltages     ! You can see zero sequence voltages with this report (about 1%)
//show currents resid=yes elements
//Show Powers kva Elements    ! Note phase powers are more normal using this method
! However, kvars  in reactor are lower than specified due to low voltage

